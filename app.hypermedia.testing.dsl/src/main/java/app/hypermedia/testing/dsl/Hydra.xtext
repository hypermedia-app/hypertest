grammar app.hypermedia.testing.dsl.Hydra with app.hypermedia.testing.dsl.Core
    
import "http://testing.hypermedia.app/dsl/core"
generate hydra "http://testing.hypermedia.app/dsl/hydra"

HydraScenario:
    (namespaces+=NamespaceDeclaration)*
    (steps+=TopLevelStep)*;
 
@Override 
TopLevelStep:
    super |
    RelaxedOperationBlock;
    
@Override 
RepresentationStep:
	super |
	OperationBlock
;

@Override 
Identifier: 
    UriName | 
    PrefixedName
;

NamespaceDeclaration:
    'PREFIX' prefix=PrefixDeclaration ':' namespace=URI
;

PrefixDeclaration: value=STRING;
UriName: value=URI;
PrefixedName: value=QNAME;

RelaxedOperationBlock:
    WithModifier 'Operation' name=Identifier '{'
        (invocations+=InvocationBlock)*
    '}'
;

// TODO: Can we use only one step for this and restrict Expect Operation from being used on top level?? 
OperationBlock:
    modifier=Modifier 'Operation' name=Identifier '{'
        (invocations+=InvocationBlock)*
    '}'
;

InvocationBlock:
    {InvocationBlock} 'Invoke' '{'
        (children+=ResponseStep)*
    '}'
;

terminal URI:
    '<' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'>') )* '>'
;
terminal QNAME: ID ':' ID;
