grammar app.hypermedia.testing.dsl.Core with org.eclipse.xtext.common.Terminals

generate core "http://testing.hypermedia.app/dsl/core"

Model:
    (steps+=TopLevelStep)*;

TopLevelStep:
	ClassBlock |
	RelaxedLinkBlock;
	
ResponseStep:
    StatusStatement |
    RepresentationStep
;

RepresentationStep:
    PropertyBlock |
    PropertyStatement | 
    LinkStep;
    
LinkStep:
    LinkStatement |
    RelaxedLinkBlock |
    StrictLinkBlock
;

Modifier: WithModifier | ExpectModifier;

ExpectModifier: 'Expect';
WithModifier: 'With';

ClassBlock:
	WithModifier 'Class' name=STRING '{'
		(children+=RepresentationStep)*
	'}';

PropertyBlock:
    modifier=Modifier 'Property' name=STRING '{'
        (children+=RepresentationStep)*
    '}'
;

PropertyStatement:
    ExpectModifier 'Property' name=STRING (value=STRING)?
;

StatusStatement:
    ExpectModifier 'Status' status=INT
;

RelaxedLinkBlock:
    'With' 'Link' relation=STRING '{'
        (children+=ResponseStep)*
    '}'
;

StrictLinkBlock:
    'Expect' 'Link' relation=STRING '{'
        (children+=ResponseStep)*
    '}'
;

LinkStatement:
    'Expect' 'Link' relation=STRING
;
