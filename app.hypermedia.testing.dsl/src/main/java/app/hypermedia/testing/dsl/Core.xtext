grammar app.hypermedia.testing.dsl.Core with org.eclipse.xtext.common.Terminals

generate core "http://testing.hypermedia.app/dsl/core"

Model:
    (steps+=TopLevelStep)*;

TopLevelStep:
	ClassBlock |
	RelaxedLinkBlock;

ResponseStep:
    StatusStatement |
    HeaderStatement |
    RepresentationStep
;

RepresentationStep:
    PropertyBlock |
    PropertyStatement |
    LinkStep;

LinkStep:
    LinkStatement |
    RelaxedLinkBlock |
    StrictLinkBlock
;

ClassBlock:
	'With' 'Class' name=STRING '{'
		(children+=RepresentationStep)*
		hatch=HATCH?
	'}';

PropertyBlock:
    modifier=('With' | 'Expect') 'Property' name=STRING '{'
        (children+=RepresentationStep)*
        hatch=HATCH?
    '}'
;

PropertyStatement:
    'Expect' 'Property' name=STRING (value=STRING)?
;

StatusStatement:
    'Expect' 'Status' status=INT
;

RelaxedLinkBlock:
    'With' 'Link' relation=STRING '{'
        (children+=ResponseStep)*
    '}'
;

StrictLinkBlock:
    'Expect' 'Link' relation=STRING '{'
        (children+=ResponseStep)*
    '}'
;

LinkStatement:
    'Expect' 'Link' relation=STRING
;

HeaderStatement:
    'Expect' 'Header' fieldName=FIELD_NAME (exactValue=STRING | regex=MatchingRegex | variable=VARIABLE)?
;

MatchingRegex:
    'Matching' value=STRING
;

terminal VARIABLE: '[' ( ALPHA )* ']';
terminal FIELD_NAME: ALPHA ('-' | ALPHA)*;

terminal ALPHA: ('a'..'z' | 'A'..'Z');

terminal HATCH: '++' -> '++';
