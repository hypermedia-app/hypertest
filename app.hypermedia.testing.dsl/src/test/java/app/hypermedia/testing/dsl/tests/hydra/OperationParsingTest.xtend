/*
 * generated by Xtext 2.18.0
 */
package app.hypermedia.testing.dsl.tests.hydra

import app.hypermedia.testing.dsl.hydra.OperationBlock
import app.hypermedia.testing.dsl.hydra.HydraScenario
import com.google.inject.Inject
import org.eclipse.xtext.testing.InjectWith
import org.eclipse.xtext.testing.extensions.InjectionExtension
import org.eclipse.xtext.testing.util.ParseHelper
import org.junit.jupiter.api.Test
import org.junit.jupiter.api.^extension.ExtendWith
import app.hypermedia.testing.dsl.tests.HydraInjectorProvider
import static org.assertj.core.api.Assertions.*
import app.hypermedia.testing.dsl.hydra.InvocationBlock
import app.hypermedia.testing.dsl.tests.TestHelpers
import app.hypermedia.testing.dsl.core.ClassBlock
import app.hypermedia.testing.dsl.Modifier
import app.hypermedia.testing.dsl.hydra.RelaxedOperationBlock
import org.junit.jupiter.params.ParameterizedTest
import org.junit.jupiter.params.provider.MethodSource
import org.eclipse.xtext.testing.validation.ValidationTestHelper
import app.hypermedia.testing.dsl.hydra.HydraPackage

@ExtendWith(InjectionExtension)
@InjectWith(HydraInjectorProvider)
class OperationParsingTest {
    @Inject extension
    ParseHelper<HydraScenario> parseHelper
    @Inject extension ValidationTestHelper

    @Test
    def void withOperationOnTopLevel_ParsesName() {
        // when
        val result = parseHelper.parse('''
            With Operation <http://example.com/CreateUser> {

            }
        ''')

        // then
        TestHelpers.assertModelParsedSuccessfully(result)
        
        val classBlock = result.steps.get(0) as RelaxedOperationBlock
        assertThat(classBlock.name.value).isEqualTo("http://example.com/CreateUser")
    }

    @Test
    def void withOperationOnTopLevel_PrefixedName_ParsesName() {
        // when
        val result = parseHelper.parse('''
            PREFIX "schema": <http://schema.org/>

            With Operation schema:Person {

            }
        ''')

        // then
        TestHelpers.assertModelParsedSuccessfully(result)

        val classBlock = result.steps.get(0) as RelaxedOperationBlock
        assertThat(classBlock.name.value).isEqualTo("schema:Person")
    }

    @Test
    def void withOperationOnTopLevelWithInvocation_ParsesSuccessfully() {
        // when
        val result = parseHelper.parse('''
            With Operation <http://example.com/CreateUser> {
                Invoke {
                }
                
                Invoke {
                }

                Invoke {
                }
            }
        ''')

        // then
        TestHelpers.assertModelParsedSuccessfully(result)
        
        val operationBlock = result.steps.get(0) as RelaxedOperationBlock
        assertThat(operationBlock.invocations).hasSize(3)
        val invokeBlock = operationBlock.invocations.get(0)
        assertThat(invokeBlock).isInstanceOf(InvocationBlock)
    }

    @Test
    def void expectOperation_ParsesSuccessfullyWhenNested() {
        // when
        val result = parseHelper.parse('''
            With Class <http://example.com/Foo> {
                Expect Operation <http://example.com/CreateUser> {
                }
            }
        ''')

        // then
        TestHelpers.assertModelParsedSuccessfully(result)
        
        val classBlock = result.steps.get(0) as ClassBlock
        val operationBlock = classBlock.children.get(0) as OperationBlock
        assertThat(operationBlock).isInstanceOf(OperationBlock)
        assertThat(operationBlock.modifier).isEqualTo(Modifier.EXPECT)
    }

    @Test
    def void expectOperation_ParsingFailsOnTopLevel() {
        // when
        val result = parseHelper.parse('''
            Expect Operation <http://example.com/CreateUser> {
            }
        ''')

        // then
       assertThat(result).isNull()
    }

    @Test
    def void withOperation_ParsesSuccessfullyWhenNested() {
        // when
        val result = parseHelper.parse('''
            With Class <http://example.com/Foo> {
                With Operation <http://example.com/CreateUser> {
                }
            }
        ''')

        // then
        TestHelpers.assertModelParsedSuccessfully(result)
        
        val classBlock = result.steps.get(0) as ClassBlock
        val operationBlock = classBlock.children.get(0) as OperationBlock
        assertThat(operationBlock).isInstanceOf(OperationBlock)
        assertThat(operationBlock.modifier).isEqualTo(Modifier.WITH)
    }

    @ParameterizedTest
    @MethodSource("app.hypermedia.testing.dsl.tests.hydra.TestCases#invalidUris")
    def void topOperationWithInvalidUri_failsValidation(String id) {
        // when
        val result = '''
            With Operation <«id»> { }
        '''.parse

        // then
        result.assertError(
            HydraPackage.Literals.RELAXED_OPERATION_BLOCK,
            null,
            "Value is not a valid URI"
        )
    }

    @ParameterizedTest
    @MethodSource("app.hypermedia.testing.dsl.tests.hydra.TestCases#validUris")
    def void topOperationWithValidUri_passesValidation(String id) {
        // when
        val result = '''
            With Class <«id»> { }
        '''.parse

        // then
        result.assertNoIssues()
    }

    @ParameterizedTest
    @MethodSource("app.hypermedia.testing.dsl.tests.hydra.TestCases#invalidUris")
    def void operationWithInvalidUri_failsValidation(String id) {
        // when
        val result = '''
            With Class some:class { With Operation <«id»> {} }
        '''.parse

        // then
        result.assertError(
            HydraPackage.Literals.OPERATION_BLOCK,
            null,
            "Value is not a valid URI"
        )
    }

    @ParameterizedTest
    @MethodSource("app.hypermedia.testing.dsl.tests.hydra.TestCases#validUris")
    def void oeprationWithValidUri_passesValidation(String id) {
        // when
        val result = '''
            With Class some:class { With Operation <«id»> {} }
        '''.parse

        // then
        result.assertNoIssues()
    }
}
